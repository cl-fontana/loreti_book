hist/del 0
vect/del *
set *
igset *
opt *

fort/file 66 duegau.eps
graph/meta 66 -113

opt nbox
opt utit
set *fon -130                           | times-roman,
set *siz 0.4                            | font height (cm)
igset txfp -130
igset chhe 0.4
set fwid 2

color  9 .91 .91 .91                    | Set color  9 to gray91
color 10 .61 .61 .61                    | Set color 10 to gray61

graph/hplot/null -1.5 2.5 0 1.5

| Filled zones under the two functions

set fcol 1109
set dmod 1
func/plot 1.329807601*exp(-5.555555556*x*x) 0.59 2.5 s
set fcol 1110
func/plot 1.329807601*exp(-5.555555556*(x-1)**2) -1.5 0.61 s

| Mean values

set dmod 14
graph/prim/line 0 0 0 1.329807601
set dmod 14
graph/prim/line 1 0 1 1.329807601

| c-line

set dmod 12
graph/prim/line 0.6 0 0.6 1.5

| Function contours

set fcol
set dmod 1
func/plot 1.329807601*exp(-5.555555556*x*x)      -1.5 2.5 s
set dmod 1
func/plot 1.329807601*exp(-5.555555556*(x-1)**2) -1.5 2.5 s

| c value

graph/prim/itx 0.4 1.4 'c = 0.6'

| upper-right box

igset chhe 0.3
set lwid
set plci 1
graph/prim/box 1.4 2.5 1.25 1.5

igset fais 1                    | solid filled
igset faci 10
graph/prim/box 1.5 1.7 1.3 1.35
igset fais 0                    | empty
igset faci 1                    | black
graph/prim/box 1.5 1.7 1.3 1.35
graph/prim/itx 1.75 1.31 'Errori di tipo II'
igset fais 1
igset faci 9
graph/prim/box 1.5 1.7 1.4 1.45
igset fais 0
igset faci 1
graph/prim/box 1.5 1.7 1.4 1.45
graph/prim/itx 1.75 1.41 'Errori di tipo I'

fort/close 66
opt htit
