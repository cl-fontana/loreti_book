HIST/DEL 0
VECT/DEL *
SET *
IGSET *

FORT/FILE 66 CHI.EPS
GRAPH/META 66 -113                      | -113: Encapsulated PostScript

OPT NBOX
OPT UTIT
OPT GRID
SET VFON -130                           | Axis values: Times-Roman,
SET VSIZ 0.4                            |       font height (cm)
IGSET TXFP -130                         | ITX text font: Times-Roman
IGSET CHHE 0.4                          | ITX text size (cm)
IGSET TXAL 0 0                          | ITX text alignment (normal)
SET NDVX -504
SET NDVY -50206
GRAPH/HPLOT/NULL 0.0 20.0 0.0 0.6
IGSET FAIS 1
IGSET FACI 0
GRAPH/PRIMITIVE/BOX 13.0 20.0 0.46 0.6
IGSET FAIS 0
GRAPH/PRIMITIVE/BOX 13.0 20.0 0.46 0.6
GRAPH/PRIMITIVE/ITX 17.5 0.575 'N=1'
GRAPH/PRIMITIVE/ITX 17.5 0.550 'N=2'
GRAPH/PRIMITIVE/ITX 17.5 0.525 'N=3'
GRAPH/PRIMITIVE/ITX 17.5 0.500 'N=5'
GRAPH/PRIMITIVE/ITX 17.5 0.475 'N=10'

VECT/CREATE XL(2)  R 13.75 16.75
VECT/CREATE YL1(2) R 0.580 0.580
VECT/CREATE YL2(2) R 0.555 0.555
VECT/CREATE YL3(2) R 0.530 0.530
VECT/CREATE YL4(2) R 0.505 0.505
VECT/CREATE YL5(2) R 0.480 0.480

SET FWID 2
SET LWID 2
FUNC/PLOT 0.39894228/SQRT(X)*EXP(-0.5*X) -0.1 20.1 S            | N=1
SET DMOD 1
GRAPH/PRIM/PLINE 2 XL YL1
IGSET LTYP 12
FUNC/PLOT 0.5*EXP(-0.5*X) -0.1 20.1 S                            | N=2
SET DMOD 12
GRAPH/PRIM/PLINE 2 XL YL2
IGSET LTYP 15
FUNC/PLOT 0.39894228*SQRT(X)*EXP(-0.5*X) -0.1 20.1 S             | N=3
SET DMOD 15
GRAPH/PRIM/PLINE 2 XL YL3
IGSET LTYP 13
FUNC/PLOT 0.13298076*X**1.5*EXP(-0.5*X) -0.1 20.1 S              | N=5
SET DMOD 13
GRAPH/PRIM/PLINE 2 XL YL4
IGSET LTYP 12
SET DASH 0.1
FUNC/PLOT 0.00130208333*X**4.0*EXP(-0.5*X) -0.1 20.1 S           | N=10
SET DMOD 12
GRAPH/PRIM/PLINE 2 XL YL5

FORT/CLOSE 66
OPT HTIT
